buildscript {
    apply from: 'api/manifest.gradle'
    ext.kotlinVersion = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.android.application' version '8.1.2' apply false
    id 'com.android.library' version '8.1.2' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
    id 'dev.rikka.tools.autoresconfig' version '1.2.1' apply false
    id 'dev.rikka.tools.materialthemebuilder' version '1.3.3' apply false
    alias libs.plugins.refine apply false
    id("idea")
}

idea.module {
    excludeDirs += file('out')
}

subprojects {
    plugins.withId("com.android.base") {
        android {
            compileSdk = 36
            //buildToolsVersion = "36.0.0"
            ndkVersion = "29.0.13113456"
            defaultConfig {
                minSdk = 24
                targetSdk = 36
            }
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_21
                targetCompatibility = JavaVersion.VERSION_21
            }
        }
    }
}

apply from: 'api/manifest.gradle'
def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = "${api_version_major}.6.0"
ext {
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
}
